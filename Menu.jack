class Menu{
    static int bestScore;
    /** start Menu of the game */
    function void startMenu(){
        var int timer;
        var char key;
        let bestScore = 0;
        let timer = 0; let key = 0;
        while(~(key = 32)){
            let key = Keyboard.keyPressed();
            if(Random.mod(timer, 90)=0){
                do Output.moveCursor(11, 23);
                do Output.printString("Click Space to Start");
            }
            if(Random.mod(timer+30, 90)=0){
                do Output.moveCursor(11, 23);
                do Output.printString("                    ");
            }
            let timer = timer + 1;
            do Sys.wait(5);
        }
        do Random.setInitSeed(timer);
        return;
    }

    /* restart Menu of the game */
    function boolean restartMenu(int currScore){

        var char key;
        var int timer;
        // print the bestScore
        if(currScore = 0){
            do Output.moveCursor(10, 27);
            do Output.printString("OH NO!");
        }
        else{
            if(currScore > bestScore){
                let bestScore = currScore;
                do Output.moveCursor(10, 14);
                do Output.printString("Congratulations! You've break you record");
            }else{
                do Output.moveCursor(10, 22);
                do Output.printString("Your Best Score: ");
                do Output.printInt(bestScore);
            }
        }
        // print the score of User
        do Output.moveCursor(11, 24);
        do Output.printString("Your score: ");
        do Output.printInt(currScore);
        // more information
        do Output.moveCursor(12, 22);
        do Output.printString("Press <Esc> to Quit");
        do Output.moveCursor(13, 14);
        do Output.printString("Press any Key to Restart the Game  :)");
        do Sys.wait(500);
        while(key = 0){
            let key = Keyboard.keyPressed();
            let timer = timer + 1;
            do Sys.wait(10);
        }
        if(key = 140){
            return true;
        }
        return false;
    }

    /** exit Menu of the Game */
    function void exitMenu(){
        do Output.moveCursor(10, 20);
        do Output.printString("Hope You Enjoy the Game");
        do Output.moveCursor(11, 24);
        do Output.printString("See You Next Time!");
        return;
    }
}